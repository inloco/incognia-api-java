plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.diffplug.spotless' version '5.12.5'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group = "com.incognia"
version = "1.0.1-SNAPSHOT"

repositories {
    mavenCentral()
}

configurations {
    testCompileOnly.extendsFrom compileOnly
    testAnnotationProcessor.extendsFrom annotationProcessor
}

ext {
    lombokVersion = '1.18.20'
}

dependencies {
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    implementation platform("com.squareup.okhttp3:okhttp-bom:4.9.1")
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.12.3"
    implementation 'com.auth0:java-jwt:3.16.0'

    testImplementation 'commons-io:commons-io:2.9.0'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'org.mockito:mockito-core:3.10.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.10.0'
    testImplementation "com.squareup.okhttp3:mockwebserver"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

compileJava {
    sourceCompatibility '1.8'
    targetCompatibility '1.8'
}

spotless {
    java {
        googleJavaFormat()
    }
}

shadowJar {
    classifier = ''
    relocate 'okhttp3', 'shadow.okhttp3'
    relocate 'com.fasterxml', 'shadow.com.fasterxml'
    relocate 'com.auth0', 'shadow.com.auth0'
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            publication.artifactId = 'incognia-api-client'
            project.shadow.component(publication)
            artifact tasks.javadocJar
            artifact tasks.sourcesJar
            pom {
                name = 'Incognia API Client'
                description = "Java client library for Incognia's API"
                url = 'https://github.com/inloco/incognia-api-java'
                developers {
                    developer {
                        id = 'racevedoo'
                        name = 'Rafael Acevedo'
                        email = 'rafael.acevedo@incognia.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/inloco/incognia-api-java.git'
                    developerConnection = 'scm:git:ssh://github.com/inloco/incognia-api-java.git'
                    url = 'https://github.com/inloco/incognia-api-java'
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}
